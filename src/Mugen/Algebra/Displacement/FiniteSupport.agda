module Mugen.Algebra.Displacement.FiniteSupport where

open import 1Lab.Reflection.Record

open import Algebra.Magma
open import Algebra.Monoid
open import Algebra.Semigroup

open import Mugen.Prelude

open import Mugen.Algebra.Displacement
open import Mugen.Algebra.Displacement.Coimage
open import Mugen.Algebra.Displacement.InfiniteProduct
open import Mugen.Algebra.Displacement.NearlyConstant
open import Mugen.Algebra.OrderedMonoid
open import Mugen.Order.StrictOrder

open import Mugen.Data.List


--------------------------------------------------------------------------------
-- Finitely Supported Functions
-- Section 3.3.5
--
-- Finitely supported functions over some displacement algebra 'ğ’Ÿ' are
-- functions 'f : Nat â†’ ğ’Ÿ' that differ from 'const Îµ' in only a finite number of positions.
-- These are a special case of the Nearly Constant functions where the base is always Îµ
-- and are implemented as such.

module FinSupport {o r} (ğ’Ÿ : Displacement-algebra o r) (cmp : âˆ€ x y â†’ Tri (Displacement-algebra._<_ ğ’Ÿ) x y) where
  private
    module ğ’Ÿ = Displacement-algebra ğ’Ÿ
    open ğ’Ÿ using (Îµ; _âŠ—_; _<_; _â‰¤_)
  open NearlyConst ğ’Ÿ cmp public

  --------------------------------------------------------------------------------
  -- Finite Support Lists
  --
  -- As noted above, these are defined to be SupportLists of nearly constant functions,
  -- with the constraint that the base is 'Îµ'.

  record FinSupportList : Type o where
    no-eta-equality
    field
      support : SupportList

    open SupportList support public

    field
      is-Îµ : base â‰¡ Îµ

  open FinSupportList

  -- Paths between finitely supportd functions are purely determined by their elements.
  fin-support-list-path : âˆ€ {xs ys} â†’ xs .support â‰¡ ys .support â†’ xs â‰¡ ys
  fin-support-list-path p i .support = p i
  fin-support-list-path {xs = xs} {ys = ys} p i .is-Îµ =
    is-setâ†’squarep (Î» _ _ â†’ ğ’Ÿ.has-is-set) (ap SupportList.base p) (xs .is-Îµ) (ys .is-Îµ) refl i

  private unquoteDecl eqv = declare-record-iso eqv (quote FinSupportList)

  FinSupportList-is-set : is-set FinSupportList
  FinSupportList-is-set =
    is-hlevelâ‰ƒ 2 (Isoâ†’Equiv eqv) $
      Î£-is-hlevel 2 SupportList-is-set Î» support â†’
        is-hlevel-suc 2 ğ’Ÿ.has-is-set (SupportList.base support) Îµ

  merge-fin : FinSupportList â†’ FinSupportList â†’ FinSupportList
  merge-fin xs ys .FinSupportList.support = merge (xs .support) (ys .support)
  merge-fin xs ys .FinSupportList.is-Îµ = apâ‚‚ _âŠ—_ (xs .is-Îµ) (ys .is-Îµ) âˆ™ ğ’Ÿ.idl
  
  empty-fin : FinSupportList
  empty-fin .support = empty
  empty-fin .is-Îµ = refl

  --------------------------------------------------------------------------------
  -- Algebra


  --------------------------------------------------------------------------------
  -- Order

  _merge-fin<_ : FinSupportList â†’ FinSupportList â†’ Type (o âŠ” r)
  _merge-fin<_ xs ys = (xs .support) merge< (ys .support)


--------------------------------------------------------------------------------
-- Displacement Algebra

module _ {o r} (ğ’Ÿ : Displacement-algebra o r) (cmp : âˆ€ x y â†’ Tri (Displacement-algebra._<_ ğ’Ÿ) x y) where
  open FinSupport ğ’Ÿ cmp
  open FinSupportList

  FiniteSupport : Displacement-algebra o (o âŠ” r)
  FiniteSupport = to-displacement-algebra mk where
    mk-strict : make-strict-order (o âŠ” r) FinSupportList
    mk-strict .make-strict-order._<_ = _merge-fin<_
    mk-strict .make-strict-order.<-irrefl {xs} =
      merge-list<-irrefl (base xs) (list xs)
    mk-strict .make-strict-order.<-trans {xs} {ys} {zs} =
      merge-list<-trans (base xs) (list xs) (base ys) (list ys) (base zs) (list zs)
    mk-strict .make-strict-order.<-thin {xs} {ys} =
      merge-list<-is-prop (base xs) (list xs) (base ys) (list ys)
    mk-strict .make-strict-order.has-is-set = FinSupportList-is-set

    mk : make-displacement-algebra (to-strict-order mk-strict)
    mk .make-displacement-algebra.Îµ = empty-fin
    mk .make-displacement-algebra._âŠ—_ = merge-fin
    mk .make-displacement-algebra.idl {xs} =
      fin-support-list-path (merge-idl (xs .support))
    mk .make-displacement-algebra.idr {xs} =
      fin-support-list-path (merge-idr (xs .support))
    mk .make-displacement-algebra.associative {xs} {ys} {zs} =
      fin-support-list-path (merge-assoc (xs .support) (ys .support) (zs .support))
    mk .make-displacement-algebra.left-invariant {xs} {ys} {zs} =
      merge-left-invariant (support xs) (support ys) (support zs)

--------------------------------------------------------------------------------
-- Subalgebra Structure

module _
  {o r}
  {ğ’Ÿ : Displacement-algebra o r}
  (let module ğ’Ÿ = Displacement-algebra ğ’Ÿ)
  (cmp : âˆ€ x y â†’ Tri ğ’Ÿ._<_ x y)
  where
  open FinSupport ğ’Ÿ cmp
  open FinSupportList
  
  FinSupportâŠ†NearlyConstant : is-displacement-subalgebra (FiniteSupport ğ’Ÿ cmp) (NearlyConstant ğ’Ÿ cmp)
  FinSupportâŠ†NearlyConstant = to-displacement-subalgebra mk where
    mk : make-displacement-subalgebra _ _
    mk .make-displacement-subalgebra.into = support
    mk .make-displacement-subalgebra.pres-Îµ = refl
    mk .make-displacement-subalgebra.pres-âŠ— _ _ = refl
    mk .make-displacement-subalgebra.strictly-mono _ _ xs<ys = xs<ys
    mk .make-displacement-subalgebra.inj = fin-support-list-path

  FinSupportâŠ†InfProd : is-displacement-subalgebra (FiniteSupport ğ’Ÿ cmp) (InfProd ğ’Ÿ)
  FinSupportâŠ†InfProd =
    is-displacement-subalgebra-trans
      FinSupportâŠ†NearlyConstant
      (NearlyConstantâŠ†InfProd cmp)

--------------------------------------------------------------------------------
-- Ordered Monoid

module _
  {o r}
  {ğ’Ÿ : Displacement-algebra o r}
  (let module ğ’Ÿ = Displacement-algebra ğ’Ÿ)
  (ğ’Ÿ-ordered-monoid : has-ordered-monoid ğ’Ÿ)
  (cmp : âˆ€ x y â†’ Tri ğ’Ÿ._<_ x y)
  where
  open FinSupport ğ’Ÿ cmp
  open FinSupportList
  open NearlyConst ğ’Ÿ cmp
  open is-ordered-monoid ğ’Ÿ-ordered-monoid

  fin-support-ordered-monoid : has-ordered-monoid (FiniteSupport ğ’Ÿ cmp)
  fin-support-ordered-monoid = right-invariantâ†’has-ordered-monoid (FiniteSupport ğ’Ÿ cmp) Î» {xs} {ys} {zs} xsâ‰¤ys â†’
    âŠ-mapl fin-support-list-path (merge-right-invariant ğ’Ÿ-ordered-monoid cmp (support xs) (support ys) (support zs) (âŠ-mapl (ap support) xsâ‰¤ys))
